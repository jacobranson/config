# refs:
#   - http://redsymbol.net/articles/unofficial-bash-strict-mode/

_default:
  @just --justfile /etc/Justfile --list --unsorted

# For first time setup of a new system. Decrypts user ssh
# private key, determines public key, and configures them both.
_setup-ssh:
  #!/usr/bin/env bash
  set -euo pipefail
  IFS=$'\n\t'

  key="$HOME/.ssh/id_ed25519"
  sudo cat /run/agenix/users/$(whoami)/id_ed25519 > "$key"
  chmod 600 "$key"
  ssh-keygen -f "$key" -y > "$key.pub"
  chmod 644 "$key.pub"

# For first time setup of a new system. Enrolls the luks decryption
# password into TPM, removing the need to enter the disk encryption
# password on each boot.
_setup-tpm device="/dev/nvme0n1p3":
  sudo systemd-cryptenroll \
    --tpm2-device=auto \
    --tpm2-pcrs=0+2+7+12 \
    --wipe-slot=tpm2 \
    {{device}}

# Shows all fonts currently installed.
show-fonts:
  fc-list : family | sort | uniq | less

# Shows all GNOME extensions currently installed.
show-gnome-extensions:
  gnome-extensions list | less

# Shows all .desktop files from installed Nix and Flatpak apps.
show-desktop-files:
  #!/usr/bin/env bash
  set -euo pipefail
  IFS=$'\n\t'

  sudo ls -A1 2> /dev/null \
    /run/current-system/sw/share/applications/*.desktop \
    /var/lib/flatpak/app/*/current/active/files/share/applications/*.desktop \
  | while read path; do basename "$path"; done | less || true

# Shows all IOMMU devices.
show-iommu:
  #! /usr/bin/env bash
  set -euo pipefail
  IFS=$'\n\t'

  shopt -s nullglob
  for d in /sys/kernel/iommu_groups/*/devices/*; do
      n=${d#*/iommu_groups/*}; n=${n%%/*}
      printf 'IOMMU Group %s ' "$n"
      lspci -nns "${d##*/}"
  done | less;

# Saves monitor configuration to the persistent partition.
save-monitors-config:
  cp ~/.config/monitors.xml "/persist/${HOME}/.config"

# Loads monitor configuration from the persistent partition.
load-monitors-config:
  cp "/persist/${HOME}/.config/monitors.xml" ~/.config

# Watch for changes made to the dconf database.
watch-dconf:
  dconf watch /
